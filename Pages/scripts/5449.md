---
pid:            5449
author:         greg zakharov
title:          Download Firefox
date:           2014-09-18 15:40:50
format:         text
parent:         0

---

# Download Firefox

### [download](//scripts/5449.txt)



```text
@echo off
  setlocal
    set /p "des=Enter download path: "
    if not exist %des% (
      echo.Folder does not exist.
      goto:eof
    )
    for /f "tokens=1,* delims=][" %%i in (
      'dir %des% ^| find /n /v ""'
    ) do (
      if "%%i" equ "6" set "itm=%%j"
    )
    set "itm=%itm:~-1,1%"
    if "%itm%" neq "." (
      echo.Folder name is required.
      goto:eof
    )
    set /p "bld=Enter major version: "
    if "%bld%" equ "" (
      echo.Version did not specified.
      goto:eof
    )
    ::check full version of build
    set "log=%tmp%\ftp.log"
    set "pub=pub/firefox/releases/"
    >"%log%" (
      echo.open ftp.mozilla.org
      echo.ls %pub:~0,20%
      echo.bye
    )
    ::looking for specified version
    >>"%log%" 2>&1 (
      ftp -Avis:"%log%"
    )
    for /f "tokens=4 delims=/" %%i in (
      'findstr /rc:"\<[0-9]$" "%log%"'
    ) do (
      for /f "tokens=1,* delims=." %%j in ("%%i") do (
        if "%%j" equ "%bld%" set "num=%%j.%%k"
      )
    )
    if "%num%" equ "" (
      echo.Selected version has been not found.
      del /f /q "%log%"
      goto:eof
    )
    echo.Current version is %num%
    ::ask about loclization
    set /p "loc=Enter locale (default is en-US): "
    if "%loc%" equ "" (
      echo.Defualt localtization will be download.
      set "loc=en-US"
    )
    ::rewrite script and download
    pushd %des%
    cd.>"%log%"
    >"%log%" (
      echo.open ftp.mozilla.org
      echo.bin
      echo.hash
      echo.lcd %des%
      echo.get "%pub%%num%/win32/%loc%/Firefox Setup %num%.exe"
      echo.bye
    )
    ftp -Avis:"%log%"
    popd
    ::remove temporary file at last
    del /f /q "%log%"
    cls
  endlocal
exit /b
```
