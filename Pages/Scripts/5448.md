---
pid:            5448
author:         greg zakharov
title:          Download latest Opera
date:           2014-09-18 15:38:50
format:         text
parent:         0

---

# Download latest Opera

### [download](Scripts\5448.txt)



```text
@echo off
  setlocal
    ::ask download path
    set /p "des=Enter download path: "
    if not exist %des% (
      echo.Folder does not exist.
      goto:eof
    )
    ::check that specified path is not a file
    for /f "tokens=1,* delims=][" %%i in (
      'dir %des% ^| find /n /v ""'
    ) do (
      if "%%i" equ "6" set "itm=%%j"
    )
    set "itm=%itm:~-1,1%"
    if "%itm%" neq "." (
      echo.Folder name is required.
      goto:eof
    )
    ::if all OK then generate ftp script
    set "log=%tmp%\ftp.log"
    set "pub=pub/opera/desktop/"
    >"%log%" (
      echo.open ftp.opera.com
      echo.ls %pub:~0,17%
      echo bye
    )
    ::scan latest version
    >>"%log%" 2>&1 (
      ftp -Avis:"%log%"
    )
    for /f "tokens=4 delims=/" %%i in (
      'findstr pub "%log%"'
    ) do set "v=%%i"
    pushd %des%
    ::rewrite script
    cd.>"%log%"
    >"%log%" (
      echo.open ftp.opera.com
      echo.bin
      echo.hash
      echo.lcd %des%
      echo.get %pub%%v%/win/Opera_%v%_Setup.exe
      echo.bye
    )
    ::downloading
    ftp -Avis:"%log%"
    popd
    ::remove temporary file at last
    del /f /q "%log%"
    cls
  endlocal
exit /b
```
