---
pid:            5420
author:         greg zakharov
title:          uptime.cmd
date:           2014-09-12 07:41:07
format:         text
parent:         5417

---

# uptime.cmd

### [download](Scripts\5420.txt) - [parent](Scripts\5417.md)

fix 1

```text
@echo off
  setlocal
    for /f "tokens=2 delims=," %%i in (
      'typeperf "\System\System Up Time" -sc 1 ^| findstr /rc:"\:"'
    ) do set "sec=%%i"
    set "sec=%sec:"=%"
    for /f "tokens=1 delims=." %%i in ("%sec%") do set "t=%%i"
    set /a "ss=t%%60", "t/=60", "mm=t%%60", "t/=60", "hh=t%%24", "dd=t/24"
    if %hh% lss 10 set "hh=0%hh%"
    if %mm% lss 10 set "mm=0%mm%"
    if %ss% lss 10 set "ss=0%ss%"
    echo.Days: %dd%   Time: %hh%:%mm%:%ss%
  endlocal
exit /b

```
