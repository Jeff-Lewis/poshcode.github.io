---
pid:            5422
parent:         0
children:       
poster:         greg zakharov
title:          uname.cmd
date:           2014-09-12 16:14:53
description:    Analog of UNIX uname tool. Script represents its own output in next format:
system_type: os_name extended_version_data # install_date
format:         text
---

# uname.cmd

### [download](5422.txt)  

Analog of UNIX uname tool. Script represents its own output in next format:
system_type: os_name extended_version_data # install_date

```text
@echo off
  setlocal
    set "key=HKLM\SYSTEM\CurrentControlSet\Control\ProductOptions"
    for /f "skip=3 tokens=3" %%i in (
      'reg query %key% /v ProductType'
    ) do (call:enum %%i)
    set key="HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion"
    call:getValue ProductName
    call:getValue CurrentVersion
    call:getValue BuildLab
    
    for /f "tokens=3" %%i in (
      'reg query %key% /v InstallDate'
    ) do set /a "t=%%i"
    set /a "ss=t%%60,t/=60,mm=t%%60,t/=60,hh=t%%24,dd/=24,t/=24"
    set /a "a=t+2472632,b=4*a+3,b/=146097,c=-b*146097,c/=4,c+=a"
    set /a "d=4*c+3,d/=1461,e=-1461*d,e/=4,e+=c,m=5*e+2,m/=153"
    set /a "dd=153*m+2,dd/=5,dd=-dd+e+1,Mt=-m/10,Mt*=12,Mt+=m+3"
    set /a "yy=b*100+d-4800+m/10"
    if %dd% lss 10 set "dd=0%dd%"
    if %Mt% lss 10 set "Mt=0%Mt%"
    if %hh% lss 10 set "hh=0%hh%"
    if %mm% lss 10 set "mm=0%mm%"
    if %ss% lss 10 set "ss=0%ss%"
    set "raw=%raw%# %dd%/%Mt%/%yy% %hh%:%mm%:%ss%"
    echo.%raw%
  endlocal
exit /b

:getValue %1
  for /f "skip=4 tokens=2,*" %%i in (
    'reg query %key% /v %1'
  ) do (
    if "%1" equ "CurrentVersion" (
      set "raw=%raw%%%j."
    ) else (
      set "raw=%raw%%%j "
    )
  )
exit /b 0

:enum %1
  for /f "skip=1 tokens=1,2 delims=#=" %%i in (
    'findstr "#" "%~f0"'
  ) do (
    if "%%j" equ "%1" set "raw=%%i: "
  )
exit /b

#Workstation=WinNT
#Server=ServerNT
#DomainController=LanmanNT
```
