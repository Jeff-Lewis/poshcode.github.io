---
pid:            5408
poster:         greg zakharov
title:          device_model.cmd
date:           2014-09-08 14:03:29
format:         text
parent:         0
parent:         0

---

# device_model.cmd

### [download](5408.txt)



```text
@echo off
  setlocal
    call:getKey processor
    for /f "skip=2 tokens=2,*" %%i in (
      'reg query %key% /s ^| findstr /irc:"processor"'
    ) do set "itm=CPU:   %%j"
    echo %itm%
    
    call:getKey video
    for /f "tokens=3,*" %%i in (
      'reg query %key% /s ^| findstr /ic:"device" ^| sort /r'
    ) do set "itm=Video: %%j"
    echo %itm%
    
    call:getKey set
    for /f "skip=2 tokens=3" %%i in (
      'reg query %key%\Services\Disk\Enum /v 0'
    ) do (
      for /f "skip=2 tokens=2,*" %%j in (
        'reg query "%key%\Enum\%%i" /v FriendlyName'
      ) do echo HDD:   %%k
    )
  endlocal
exit /b

:getKey %1
  for /f %%i in (
    'type "%~f0" ^| findstr /irc:"^h.*\%1$"'
  ) do set "key=%%i"
exit /b 0

HKLM\HARDWARE\DESCRIPTION\System\CentralProcessor
HKLM\SYSTEM\CurrentControlSet
HKLM\SYSTEM\CurrentControlSet\Control\Video
```
